// 02_1.grif - GRIF format
background.intro
	@if @isfalse(@get(value.introdone)) @then
		@set(value.introdone,true)
		@writeline("Welcome to ",@get(system.gametitle))
		@nl
	@endif
command.?
	@comment("get the input, a range of two values separated by -")
	@comment("change to a list separated by commas")
	@set(value.range,@replace(@get(input.full),"-",","))
	@comment("output all the values in the range")
	@for(i,@getlist(value.range,0),@getlist(value.range,1))
		@comment("values are invalid if the first half equals the second half")
		@if @eq(@mod(@len($i),2),0) @then
			@set(value.first,@substring($i,0,@div(@len($i),2)))
			@set(value.second,@substring($i,@div(@len($i),2)))
			@if @eq(@get(value.first),@get(value.second)) @then
				@writeline($i,": ",@get(value.first)," - ",@get(value.second)," - Invalid")
				@addto(value.answer,$i)
			@endif
		@endif
	@endfor
	@nl
command.end
	@writeline("Final answer: ",@get(value.answer))
	@setoutchannel("#GAMEOVER;")
message.invalid
	Invalid input
system.gamename
	AoC2025_2_1
system.gametitle
	Advent of Code 2025 - Day 2 - Puzzle 1
value.answer
	0
value.introdone
	false
verb.end
	end,quit,exit,done
