// 02_2.grif - GRIF format
background.intro
	@if @isfalse(@get(value.introdone)) @then
		@set(value.introdone,true)
		@writeline("Welcome to ",@get(system.gametitle))
		@nl
	@endif
command.?
	@comment("get the input, a range of two values separated by -")
	@comment("change to a list separated by commas")
	@set(value.range,@replace(@get(input.full),"-",","))
	@comment("output all the values in the range")
	@for(i,@getlist(value.range,0),@getlist(value.range,1))
		@comment("values are invalid if they only consist of repeated strings")
		@set(temp.found,false)
		@for(x,1,@div(@len($i),2))
			@if @false(@get(temp.found)) @then
				@set(temp.sub,@substring($i,0,$x))
				@set(temp.value,"")
				@for(z,0,@sub(@div(@len($i),$x),1))
					@set(temp.value,@concat(@get(temp.value),@get(temp.sub)))
				@endfor
				@if @false(@get(temp.found)) @and @eq($i,@get(temp.value)) @then
					@set(temp.found,true)
					@writeline($i," - Invalid")
					@addto(value.answer,$i)
				@endif
			@endif
		@endfor
	@endfor
	@nl
command.end
	@writeline("Final answer: ",@get(value.answer))
	@setoutchannel("#GAMEOVER;")
message.invalid
	Invalid input
system.gamename
	AoC_2025_02_2
system.gametitle
	Advent of Code 2025 - Day 2 - Puzzle 2
value.answer
	0
value.introdone
	false
verb.end
	end,quit,exit,done
