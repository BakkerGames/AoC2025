// 01_1.grif - GRIF format
background.intro
	@if @isfalse(@get(value.introdone)) @then
		@set(value.introdone,true)
		@writeline("Welcome to ",@get(system.gametitle))
		@nl
	@endif
command.?
	@if @lt(@len(@get(input.full)),2) @then
		@msg(message.invalid)
	@else
		@set(value.dir,@substring(@get(input.full),0,1))
		@set(value.clicks,@substring(@get(input.full),1))
		@if @ne(@get(value.dir),"L") @and @ne(@get(value.dir),"R") @then
			@msg(message.invalid)
		@elseif @not @isnumber(@get(value.clicks)) @then
			@msg(message.invalid)
		@else
			@if @eq(@get(value.dir),"L") @then
				@negto(value.clicks)
			@endif
			@addto(value.dial,@get(value.clicks))
			@modto(value.dial,100)
			@if @eq(@get(value.dial),0) @then
				@addto(value.answer,1)
			@endif
			@writeline("clicks: ",@get(value.clicks))
			@writeline("dial:   ",@get(value.dial))
			@writeline("answer: ",@get(value.answer))
			@nl
		@endif
	@endif
command.end
	@writeline("Final answer: ",@get(value.answer))
	@setoutchannel("#GAMEOVER;")
message.invalid
	Invalid input
system.gamename
	AoC2025_01_1
system.gametitle
	Advent of Code 2025 - Day 1 - Puzzle 1
value.answer
	0
value.dial
	50
value.introdone
	false
verb.end
	end,quit,exit,done
